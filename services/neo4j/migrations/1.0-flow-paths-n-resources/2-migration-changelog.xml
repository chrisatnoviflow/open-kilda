<?xml version="1.0" encoding="UTF-8"?>
<changelog xmlns:xsi="http://www.w3.org/2001/XMLSchema-instance"
           xsi:noNamespaceSchemaLocation="http://www.liquigraph.org/schema/1.0/liquigraph.xsd">
    <changeset id="1.0-create-flow-paths-n-resources" author="Sergii Iakovenko">
        <precondition if-not-met="FAIL">
            <!-- Validate flows, no orphans entities or invalid cookies allowed. -->
            <and>
                <query><![CDATA[
                       MATCH (src:switch)-[forward:flow]->(dst:switch)
                       OPTIONAL MATCH (dst)-[reverse:flow {
                               flowid: forward.flowid
                           }]->(src)
                       WITH forward, reverse
                       WHERE reverse.flowid IS NULL
                       RETURN count(forward) = 0 as result
                ]]></query>
                <query><![CDATA[
                       MATCH (src:switch)-[forward:flow]->(dst:switch)
                       MATCH (dst)-[reverse:flow {
                               flowid: forward.flowid
                           }]->(src)
                       WHERE (src.name <> dst.name
                           OR forward.src_port = reverse.dst_port AND forward.src_vlan = reverse.dst_vlan)
                           AND (forward.cookie < 4611686018427387904 AND reverse.cookie < 4611686018427387904
                           OR forward.cookie > 4611686018427387904 AND reverse.cookie > 4611686018427387904)
                       RETURN count(forward) = 0 AND count(reverse) = 0 as result
                ]]></query>
            </and>
        </precondition>

        <!-- Create a forward path for each flow with a meter. -->
        <query><![CDATA[
            MATCH (src:switch)-[forward:flow]->(dst:switch)
            WHERE forward.cookie > 4611686018427387904 AND forward.meter_id IS NOT NULL
            WITH src, dst, forward
            MERGE (src)-[:flow_path {
                    path_id: forward.flowid + "_forward", 
                    flow_id: forward.flowid, 
                    cookie: forward.cookie, 
                    meter_id: forward.meter_id, 
                    bandwidth: forward.bandwidth, 
                    ignore_bandwidth: forward.ignore_bandwidth, 
                    time_create: forward.time_modify, 
                    time_modify: forward.time_modify,
                    status: "active", 
                    latency: apoc.convert.fromJsonMap(forward.flowpath).latency_ns
                }]->(dst)
        ]]></query>

        <!-- Create a forward path for each flow with no meters. -->
        <query><![CDATA[
            MATCH (src:switch)-[forward:flow]->(dst:switch)
            WHERE forward.cookie > 4611686018427387904 AND forward.meter_id IS NULL
            WITH src, dst, forward
            MERGE (src)-[:flow_path {
                    path_id: forward.flowid + "_forward",
                    flow_id: forward.flowid,
                    cookie: forward.cookie,
                    bandwidth: forward.bandwidth,
                    ignore_bandwidth: forward.ignore_bandwidth,
                    time_create: forward.time_modify,
                    time_modify: forward.time_modify,
                    status: "active",
                    latency: apoc.convert.fromJsonMap(forward.flowpath).latency_ns
                }]->(dst)
        ]]></query>

        <!-- Create a reverse path for each flow with a meter. -->
        <query><![CDATA[
            MATCH (src:switch)-[reverse:flow]->(dst:switch)
            WHERE reverse.cookie < 4611686018427387904 AND reverse.meter_id IS NOT NULL
            WITH src, dst, reverse
            MERGE (src)-[:flow_path {
                    path_id: reverse.flowid + "_reverse", 
                    flow_id: reverse.flowid, 
                    cookie: reverse.cookie, 
                    meter_id: reverse.meter_id, 
                    bandwidth: reverse.bandwidth, 
                    ignore_bandwidth: reverse.ignore_bandwidth, 
                    time_create: reverse.time_modify, 
                    time_modify: reverse.time_modify,
                    status: "active", 
                    latency: apoc.convert.fromJsonMap(reverse.flowpath).latency_ns
                }]->(dst)
        ]]></query>

        <!-- Create a reverse path for each flow with no meters. -->
        <query><![CDATA[
            MATCH (src:switch)-[reverse:flow]->(dst:switch)
            WHERE reverse.cookie < 4611686018427387904 AND reverse.meter_id IS NULL
            WITH src, dst, reverse
            MERGE (src)-[:flow_path {
                    path_id: reverse.flowid + "_reverse",
                    flow_id: reverse.flowid,
                    cookie: reverse.cookie,
                    bandwidth: reverse.bandwidth,
                    ignore_bandwidth: reverse.ignore_bandwidth,
                    time_create: reverse.time_modify,
                    time_modify: reverse.time_modify,
                    status: "active",
                    latency: apoc.convert.fromJsonMap(reverse.flowpath).latency_ns
                }]->(dst)
        ]]></query>

        <!-- Create a flow cookie entity for each flow. -->
        <query><![CDATA[
            MATCH (src:switch)-[forward:flow]->(dst:switch)
            WHERE forward.cookie > 4611686018427387904
            WITH src, dst, forward
            MERGE (:flow_cookie {
                    flow_id: forward.flowid, 
                    unmasked_cookie: forward.cookie - 4611686018427387904
                })
        ]]></query>

        <!-- Create a flow meter entities for each forward path. -->
        <query><![CDATA[
            MATCH (src:switch)-[forward:flow]->(dst:switch)
            WHERE forward.cookie > 4611686018427387904 AND forward.meter_id IS NOT NULL
            WITH src, dst, forward
            MERGE (src)<-[:owned_by]-(:flow_meter {
                    meter_id: forward.meter_id, 
                    flow_id: forward.flowid, 
                    path_id: forward.flowid + "_forward", 
                    unique_index: src.name + "_" + forward.meter_id
                })
        ]]></query>

        <!-- Create a transit vlan entities for each forward path. -->
        <query><![CDATA[
            MATCH (src:switch)-[forward:flow]->(dst:switch)
            WHERE forward.cookie > 4611686018427387904 AND forward.transit_vlan <> 0
            WITH src, dst, forward
            MERGE (:transit_vlan {
                    flow_id: forward.flowid,
                    path_id: forward.flowid + "_forward",
                    vlan: forward.transit_vlan
                })
        ]]></query>

        <!-- Create a flow meter entities for each reverse path. -->
        <query><![CDATA[
            MATCH (src:switch)-[reverse:flow]->(dst:switch)
            WHERE reverse.cookie < 4611686018427387904 AND reverse.meter_id IS NOT NULL
            WITH src, dst, reverse
            MERGE (src)<-[:owned_by]-(:flow_meter {
                    meter_id: reverse.meter_id, 
                    flow_id: reverse.flowid, 
                    path_id: reverse.flowid + "_reverse", 
                    unique_index: src.name + "_" + reverse.meter_id
                })
        ]]></query>

        <!-- Create a transit vlan entities for each reverse path. -->
        <query><![CDATA[
            MATCH (src:switch)-[reverse:flow]->(dst:switch)
            WHERE reverse.cookie < 4611686018427387904  AND reverse.transit_vlan <> 0
            WITH src, dst, reverse
            MERGE (:transit_vlan {
                    flow_id: reverse.flowid,
                    path_id: reverse.flowid + "_reverse",
                    vlan: reverse.transit_vlan
                })
        ]]></query>
    </changeset>

    <changeset id="1.0-create-path-segments" author="Sergii Iakovenko">
        <precondition if-not-met="FAIL">
            <!-- Validate flow segments against flows. -->
            <query><![CDATA[
                MATCH ()-[fs:flow_segment]->()
                OPTIONAL MATCH ()-[f:flow {
                        flowid: fs.flowid,
                        cookie: fs.cookie
                    }]->()
                WITH f
                WHERE f.flowid IS NULL
                RETURN count(f) = 0 as result
            ]]></query>
        </precondition>

        <!-- Create path segments for each forward path. -->
        <query><![CDATA[
            MATCH (src:switch)-[forward:flow]->(dst:switch)
            WHERE forward.cookie > 4611686018427387904
            WITH src, dst, forward
            MATCH (fs_src)-[fs:flow_segment{
		    flowid: forward.flowid,
		    cookie: forward.cookie
                }]->(fs_dst)
            WITH fs_src, fs, fs_dst
            MERGE (fs_src)-[ps:path_segment {
		    path_id: fs.flowid + "_forward", 
		    src_port: fs.src_port,
		    dst_port: fs.dst_port,
		    seq_id: fs.seq_id
	        }]->(fs_dst)
            SET ps.latency = fs.segment_latency
        ]]></query>

        <!-- Create path segments for each reverse path. -->
        <query><![CDATA[
            MATCH (src:switch)-[reverse:flow]->(dst:switch)
            WHERE reverse.cookie < 4611686018427387904
            WITH src, dst, reverse
            MATCH (fs_src)-[fs:flow_segment{
		    flowid: reverse.flowid, 
		    cookie: reverse.cookie
	        }]->(fs_dst)
            WITH fs_src, fs, fs_dst
            MERGE (fs_src)-[ps:path_segment {
		    path_id: fs.flowid + "_reverse", 
		    src_port: fs.src_port, 
		    dst_port: fs.dst_port, 
		    seq_id: fs.seq_id
	        }]->(fs_dst)
            SET ps.latency = fs.segment_latency
        ]]></query>
    </changeset>

    <changeset id="1.0-clone-and-delete-reverse-flows" author="Sergii Iakovenko">
        <!-- Copy reverse flow as old_flow entitiy. -->
        <query><![CDATA[
            MATCH (src:switch)-[reverse:flow]->(dst:switch)
            WHERE reverse.cookie < 4611686018427387904
            WITH src, dst, reverse
            CREATE (src)-[of:old_flow]->(dst)
            SET of = reverse
        ]]></query>

        <!-- Delete original copies of reverse flows. -->
        <query><![CDATA[
            MATCH (src:switch)-[reverse:flow]->(dst:switch)
            WHERE reverse.cookie < 4611686018427387904
            WITH src, dst, reverse
            DELETE reverse
        ]]></query>
    </changeset>

    <changeset id="1.0-clone-and-cleanup-forward-flows" author="Sergii Iakovenko">
        <!-- Copy forward flow as old_flow entitiy. -->
        <query><![CDATA[
            MATCH (src:switch)-[forward:flow]->(dst:switch)
            WHERE forward.cookie > 4611686018427387904
            WITH src, dst, forward
            CREATE (src)-[of:old_flow]->(dst)
            SET of = forward
        ]]></query>

        <!-- Update original copies of forward flows with path ids and encapsulation type. -->
        <query><![CDATA[
            MATCH (src:switch)-[forward:flow]->(dst:switch)
            WHERE forward.cookie > 4611686018427387904
            SET forward.forward_path_id = forward.flowid + "_forward",
	            forward.reverse_path_id = forward.flowid + "_reverse",
	            forward.encapsulation_type = "transit_vlan",
	            forward.time_create = forward.time_modify
        ]]></query>

        <!-- Cleanup original copies of forward flows. -->
        <query><![CDATA[
            MATCH (src:switch)-[forward:flow]->(dst:switch)
            WHERE forward.cookie > 4611686018427387904
            REMOVE forward.flowpath,
                forward.cookie,
                forward.src_switch,
                forward.dst_switch,
                forward.last_updated,
                forward.meter_id,
                forward.transit_vlan
        ]]></query>
    </changeset>
</changelog>
